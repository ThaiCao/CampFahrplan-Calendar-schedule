apply plugin: 'com.android.application'

Properties props = new Properties()
File gradlePropertiesFile = file("gradle.properties")
if (!gradlePropertiesFile.exists()) {
    throw new IllegalStateException(
            "Please create the file 'gradle.properties' before " +
                    "importing the project. Do not forget to add custom values!"
    )
}
props.load(new FileInputStream(gradlePropertiesFile))



android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        versionCode 36
        versionName "1.31.1"
        minSdkVersion 14
        targetSdkVersion 22
        project.ext.set("archivesBaseName", "Fahrplan");
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('.apk')) {
                def fileName = file.name.replace(".apk", "-" + versionName + ".apk")
                output.outputFile = new File(file.parent, fileName)
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            zipAlignEnabled true
            debuggable true
        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    signingConfigs {
        ccc31c3 {
            storeFile file(props['signing.ccc31c3-release.keystoreFilePath'])
            storePassword props['signing.ccc31c3-release.keystorePassword']
            keyAlias props['signing.ccc31c3-release.keyAlias']
            keyPassword props['signing.ccc31c3-release.keyPassword']
        }
        camp2015 {
            storeFile file(props['signing.camp2015-release.keystoreFilePath'])
            storePassword props['signing.camp2015-release.keystorePassword']
            keyAlias props['signing.camp2015-release.keyAlias']
            keyPassword props['signing.camp2015-release.keyPassword']
        }
        froscon2015 {
            storeFile file(props['signing.froscon-release.keystoreFilePath'])
            storePassword props['signing.froscon-release.keystorePassword']
            keyAlias props['signing.froscon-release.keyAlias']
            keyPassword props['signing.froscon-release.keyPassword']
        }
    }

    productFlavors {
        ccc31c3 {
            applicationId "nerd.tuxmobil.fahrplan.congress"
            signingConfig signingConfigs.ccc31c3
            buildConfigField "String", "SCHEDULE_DOMAIN", '"events.ccc.de"'
            buildConfigField "String", "SCHEDULE_DOMAIN_PART", '"http://events.ccc.de"'
            buildConfigField "String", "SCHEDULE_EVENT_PART", '"/events/%1$s.html"'
            buildConfigField "String", "SCHEDULE_PART", '"/congress/2014/Fahrplan"'
            buildConfigField "String", "SCHEDULE_PATH", '"/congress/2014/Fahrplan/schedule.xml"'
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_YEAR", "2014"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_MONTH", "12"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_DAY", "27"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_YEAR", "2014"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_MONTH", "12"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_DAY", "31"
            buildConfigField "boolean", "SCHEDULE_SUPPORTS_HTTPS", "true"
            buildConfigField "String", "SCHEDULE_FEEDBACK_URL", '"https://frab.cccv.de/en/31C3/public/events/%s/feedback/new"'
        }
        camp2015 {
            applicationId "nerd.tuxmobil.fahrplan.camp"
            signingConfig signingConfigs.camp2015
            buildConfigField "String", "SCHEDULE_DOMAIN", '"events.ccc.de"'
            buildConfigField "String", "SCHEDULE_DOMAIN_PART", '"http://events.ccc.de"'
            buildConfigField "String", "SCHEDULE_EVENT_PART", '"/events/%1$s.html"'
            buildConfigField "String", "SCHEDULE_PART", '"/camp/2015/Fahrplan"'
            buildConfigField "String", "SCHEDULE_PATH", '"/camp/2015/Fahrplan/schedule.xml"'
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_YEAR", "2015"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_MONTH", "8"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_DAY", "13"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_YEAR", "2015"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_MONTH", "8"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_DAY", "17"
            buildConfigField "boolean", "SCHEDULE_SUPPORTS_HTTPS", "true"
            buildConfigField "String", "SCHEDULE_FEEDBACK_URL", '"https://frab.cccv.de/en/camp2015/public/events/%s/feedback/new"'
        }
        froscon2015 {
            applicationId "info.metadude.android.froscon.schedule"
            versionName "1.31.1 (FrOSCon-Edition)"
            signingConfig signingConfigs.froscon2015
            buildConfigField "String", "SCHEDULE_DOMAIN", '"programm.froscon.de"'
            buildConfigField "String", "SCHEDULE_DOMAIN_PART", '"http://programm.froscon.de"'
            buildConfigField "String", "SCHEDULE_EVENT_PART", '"/events/%1$s.html"'
            buildConfigField "String", "SCHEDULE_PART", '"/2015"'
            buildConfigField "String", "SCHEDULE_PATH", '"/2015/schedule.xml"'
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_YEAR", "2015"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_MONTH", "8"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_DAY", "22"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_YEAR", "2015"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_MONTH", "8"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_DAY", "23"
            buildConfigField "boolean", "SCHEDULE_SUPPORTS_HTTPS", "false"
            buildConfigField "String", "SCHEDULE_FEEDBACK_URL", '"https://frab.froscon.de/en/froscon2014/events/%s/feedback/new"'
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:22.2.1"
    compile 'com.afollestad:material-dialogs:0.4.6'
}

repositories {
    maven { url 'http://clinker.47deg.com/nexus/content/groups/public' }
}
