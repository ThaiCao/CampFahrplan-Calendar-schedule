apply plugin: 'com.android.application'

Properties props = new Properties()
File gradlePropertiesFile = file("gradle.properties")
if (!gradlePropertiesFile.exists()) {
    throw new IllegalStateException(
            "Please create the file 'gradle.properties' before " +
                    "importing the project. Do not forget to add custom values!"
    )
}
props.load(new FileInputStream(gradlePropertiesFile))



android {
    compileSdkVersion 19
    buildToolsVersion "21.1.1"

    defaultConfig {
        versionCode 29
        versionName "1.29.5"
        minSdkVersion 8
        targetSdkVersion 19
        project.ext.set("archivesBaseName", "Fahrplan");
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('.apk')) {
                def fileName = file.name.replace(".apk", "-" + versionName + ".apk")
                output.outputFile = new File(file.parent, fileName)
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            zipAlignEnabled true
        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    signingConfigs {
        ccc30c3 {
            storeFile file(props['signing.ccc30c3-release.keystoreFilePath'])
            storePassword props['signing.ccc30c3-release.keystorePassword']
            keyAlias props['signing.ccc30c3-release.keyAlias']
            keyPassword props['signing.ccc30c3-release.keyPassword']
        }
        fiffkon14 {
            storeFile file(props['signing.fiffkon-release.keystoreFilePath'])
            storePassword props['signing.fiffkon-release.keystorePassword']
            keyAlias props['signing.fiffkon-release.keyAlias']
            keyPassword props['signing.fiffkon-release.keyPassword']
        }
    }

    productFlavors {
        ccc30c3 {
            applicationId "nerd.tuxmobil.fahrplan.congress"
            signingConfig signingConfigs.ccc30c3
            buildConfigField "String", "SCHEDULE_DOMAIN", '"events.ccc.de"'
            buildConfigField "String", "SCHEDULE_DOMAIN_PART", '"http://events.ccc.de"'
            buildConfigField "String", "SCHEDULE_EVENT_PART", '"/events/%1$s.html"'
            buildConfigField "String", "SCHEDULE_PART", '"/congress/2013/Fahrplan"'
            buildConfigField "String", "SCHEDULE_PATH", '"/congress/2013/Fahrplan/schedule.xml"'
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_YEAR", "2013"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_MONTH", "12"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_DAY", "27"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_YEAR", "2013"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_MONTH", "12"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_DAY", "31"
            buildConfigField "boolean", "SCHEDULE_SUPPORTS_HTTPS", "true"
        }
        fiffkon14 {
            applicationId "info.metadude.android.fiffkon.schedule"
            signingConfig signingConfigs.fiffkon14
            buildConfigField "String", "SCHEDULE_DOMAIN", '"fiffkon.de"'
            buildConfigField "String", "SCHEDULE_DOMAIN_PART", '"https://fiffkon.de"'
            buildConfigField "String", "SCHEDULE_EVENT_PART", '"/events/%1$s.html"'
            buildConfigField "String", "SCHEDULE_PART", '"/app"'
            buildConfigField "String", "SCHEDULE_PATH", '"/app/schedule.xml"'
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_YEAR", "2014"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_MONTH", "11"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_DAY", "7"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_YEAR", "2014"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_MONTH", "11"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_DAY", "9"
            buildConfigField "boolean", "SCHEDULE_SUPPORTS_HTTPS", "false"
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
}
